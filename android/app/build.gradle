apply plugin: "com.android.application"
apply plugin: "org.jetbrains.kotlin.android"
apply plugin: "com.facebook.react"
// react-native-config is auto-linked, no manual include needed

/**
 * This is the configuration block to customize your React Native Android app.
 * By default you don't need to apply any configuration, just uncomment the lines you need.
 */
react {
    /* Folders */
    //   The root of your project, i.e. where "package.json" lives. Default is '../..'
    // root = file("../../")
    //   The folder where the react-native NPM package is. Default is ../../node_modules/react-native
    // reactNativeDir = file("../../node_modules/react-native")
    //   The folder where the react-native Codegen package is. Default is ../../node_modules/@react-native/codegen
    // codegenDir = file("../../node_modules/@react-native/codegen")
    //   The cli.js file which is the React Native CLI entrypoint. Default is ../../node_modules/react-native/cli.js
    // cliFile = file("../../node_modules/react-native/cli.js")

    /* Variants */
    //   The list of variants to that are debuggable. For those we're going to
    //   skip the bundling of the JS bundle and the assets. By default is just 'debug'.
    //   If you add flavors like lite, prod, etc. you'll have to list your debuggableVariants.
    // debuggableVariants = ["liteDebug", "prodDebug"]

    /* Bundling */
    //   A list containing the node command and its flags. Default is just 'node'.
    // nodeExecutableAndArgs = ["node"]
    //
    //   The command to run when bundling. By default is 'bundle'
    // bundleCommand = "ram-bundle"
    //
    //   The path to the CLI configuration file. Default is empty.
    // bundleConfig = file(../rn-cli.config.js)
    //
    //   The name of the generated asset file containing your JS bundle
    // bundleAssetName = "MyApplication.android.bundle"
    //
    //   The entry file for bundle generation. Default is 'index.android.js' or 'index.js'
    // entryFile = file("../js/MyApplication.android.js")
    //
    //   A list of extra flags to pass to the 'bundle' commands.
    //   See https://github.com/react-native-community/cli/blob/main/docs/commands.md#bundle
    // extraPackagerArgs = []

    /* Hermes Commands */
    //   The hermes compiler command to run. By default it is 'hermesc'
    // hermesCommand = "$rootDir/my-custom-hermesc/bin/hermesc"
    //
    //   The list of flags to pass to the Hermes compiler. By default is "-O", "-output-source-map"
    // hermesFlags = ["-O", "-output-source-map"]

    /* Autolinking */
    autolinkLibrariesWithApp()
}

// Ensure generated entry point references the new package name
tasks.named("generateReactNativeEntryPoint").configure {
    doLast {
        def entryPoint = file("$buildDir/generated/autolinking/src/main/java/com/facebook/react/ReactNativeApplicationEntryPoint.java")
        if (entryPoint.exists()) {
            entryPoint.text = entryPoint.text.replace("com.merchantclosepayv2", "com.solusinegeri.app")
        }
    }
}

/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = true

/**
 * The preferred build flavor of JavaScriptCore (JSC)
 *
 * For example, to use the international variant, you can use:
 * `def jscFlavor = io.github.react-native-community:jsc-android-intl:2026004.+`
 *
 * The international variant includes ICU i18n library and necessary data
 * allowing to use e.g. `Date.toLocaleString` and `String.localeCompare` that
 * give correct results when using with locales other than en-US. Note that
 * this variant is about 6MiB larger per architecture than default.
 */
def jscFlavor = 'io.github.react-native-community:jsc-android:2026004.+'

android {
    ndkVersion rootProject.ext.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdk rootProject.ext.compileSdkVersion


    // Optimize APK/AAB size
    packagingOptions {
        // Strip debug symbols from native libraries
        jniLibs {
            useLegacyPackaging = false
        }
        // Exclude unnecessary files
        resources {
            excludes += ['META-INF/LICENSE.txt', 'META-INF/NOTICE.txt', 'META-INF/*.kotlin_module']
        }
    }

    // Enable AAPT PNG crunching for smaller images
    aaptOptions {
        cruncherEnabled = true
    }

    namespace "com.solusinegeri.app"
    defaultConfig {
        applicationId "com.solusinegeri.app"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 4
        versionName "1.3"
        resValue "string", "build_config_package", "com.solusinegeri.app"
        resValue "string", "app_name", "ClosePay Member"
    }

    splits {
        abi {
            reset()
            // Disable splits for AAB builds (bundleRelease)
            // AAB handles multi-architecture automatically
            enable project.gradle.startParameter.taskNames.any { it.toLowerCase().contains('bundle') } ? false : true
            universalApk false
            include "armeabi-v7a", "arm64-v8a"
        }
    }

    // Rename APKs
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def appName = "SolusiNegeriMemberBaseApp"
            def buildType = variant.buildType.name
            def version = variant.versionName
            def abi = output.getFilter(com.android.build.OutputFile.ABI)
            
            if (abi != null) {
                outputFileName = "${appName}-${abi}-${buildType}-v${version}.apk"
            } else {
                outputFileName = "${appName}-${buildType}-v${version}.apk"
            }
        }
    }

    // Rename AABs after build
    applicationVariants.all { variant ->
        def variantName = variant.name.capitalize()
        def bundleTask = tasks.findByName("bundle${variantName}")
        
        if (bundleTask != null) {
            bundleTask.doLast {
                def appName = "SolusiNegeriMemberBaseApp"
                def buildType = variant.buildType.name
                def version = variant.versionName
                def bundleDir = file("${buildDir}/outputs/bundle/${variant.name}")
                def oldFile = file("${bundleDir}/app-${variant.name}.aab")
                def newFile = file("${bundleDir}/${appName}-${buildType}-v${version}.aab")
                
                if (oldFile.exists()) {
                    oldFile.renameTo(newFile)
                    println "AAB renamed to: ${newFile.name}"
                }
            }
        }
    }
    signingConfigs {
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            
            // Secure Configuration - Debug/Staging
            buildConfigField "String", "API_BASE_URL_PROD", "\"https://api.solusiuntuknegeri.com\""
            buildConfigField "String", "API_BASE_URL_STG", "\"https://api.stg.solusiuntuknegeri.com\""
            buildConfigField "String", "CERT_PIN_1", "\"sha256/cCZ/uMd/qFD4cMVMg8y5w99JpiGeT/sSTiPeB1mu/Ec=\""
            buildConfigField "String", "CERT_PIN_2", "\"sha256/9Fk6HgfMnM7/vtnBHcUhg1b3gU2bIpSd50XmKZkMbGA=\""
            buildConfigField "String", "SECURITY_EMAIL", "\"security@solusiuntuknegeri.com\""
        }
        release {
            // Caution! In production, you need to generate your own keystore file.
            // see https://reactnative.dev/docs/signed-apk-android.
            signingConfig signingConfigs.debug
            minifyEnabled enableProguardInReleaseBuilds
            shrinkResources enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
            
            // Secure Configuration - Production
            buildConfigField "String", "API_BASE_URL_PROD", "\"https://api.solusiuntuknegeri.com\""
            buildConfigField "String", "API_BASE_URL_STG", "\"https://api.stg.solusiuntuknegeri.com\""
            buildConfigField "String", "CERT_PIN_1", "\"sha256/cCZ/uMd/qFD4cMVMg8y5w99JpiGeT/sSTiPeB1mu/Ec=\""
            buildConfigField "String", "CERT_PIN_2", "\"sha256/9Fk6HgfMnM7/vtnBHcUhg1b3gU2bIpSd50XmKZkMbGA=\""
            buildConfigField "String", "SECURITY_EMAIL", "\"security@solusiuntuknegeri.com\""
        }
    }
}

dependencies {
    // The version of react-native is set by the React Native Gradle Plugin
    implementation("com.facebook.react:react-android")
    debugImplementation("com.facebook.flipper:flipper:${FLIPPER_VERSION}")
    debugImplementation("com.facebook.flipper:flipper-network-plugin:${FLIPPER_VERSION}") {
        exclude group:'com.squareup.okhttp3', module:'okhttp'
    }
    if (hermesEnabled.toBoolean()) {
        implementation("com.facebook.react:hermes-android")
    } else {
        implementation jscFlavor
    }

    debugImplementation "com.github.chuckerteam.chucker:library:4.0.0"
    releaseImplementation "com.github.chuckerteam.chucker:library-no-op:4.0.0"
    
    // FileProvider support
    implementation("androidx.core:core-ktx:1.12.0")
    
    // Google Tink for native crypto operations (AEAD encryption)
    implementation("com.google.crypto.tink:tink-android:1.12.0")
    
    // Talsec Security - Runtime application protection
    implementation("com.aheaditec.talsec.security:TalsecSecurity-Free:6.2.0")
}
